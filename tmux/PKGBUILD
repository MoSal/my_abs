pkgname=tmux
epoch=1
pkgver=2.1
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.github.io/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent' 'libutempter')
source=(https://github.com/tmux/tmux/releases/download/$pkgver/tmux-$pkgver.tar.gz
        dont_exit_copy_mode_with_after_copy-pipe.patch
	LICENSE)
md5sums=('74a2855695bccb51b6e301383ad4818c'
         'cf8f11d3eaf2228cc317688b96ce439d'
         '71601bc37fa44e4395580b321963018e')

prepare() {
	cd "$srcdir/$pkgname-${pkgver/_/}"
	patch -Np0 <${srcdir}/dont_exit_copy_mode_with_after_copy-pipe.patch
}

build() {
	cd "$srcdir/$pkgname-${pkgver/_/}"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-${pkgver/_/}"
	make install DESTDIR=$pkgdir
	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

	install -dm755 "$pkgdir/usr/share/tmux/"
	install -m644 examples/* "$pkgdir/usr/share/tmux/"
	install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

	install -d $pkgdir/usr/share/bash-completion/completions/
	mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}
